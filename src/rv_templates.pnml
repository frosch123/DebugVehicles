/*
 * This file is part of Debug Vehicles.
 * Debug Vehicles is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, version 2.
 * Debug Vehicles is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU General Public License for more details. You should have received a copy of the GNU General Public License along with Debug Vehicles. If not, see <http://www.gnu.org/licenses/>.
 */

#define ARTICPARTNAME(tram, veh_length) item_rvartic_##tram##_##veh_length

#define ARTICPART(id, tram, veh_length)                                               \
item(FEAT_ROADVEHS, ARTICPARTNAME(tram, veh_length), id) {                            \
    property {                                                                        \
        climates_available:             NO_CLIMATE;                                   \
        refittable_cargo_classes:       bitmask(CC_PIECE_GOODS, CC_EXPRESS);          \
        non_refittable_cargo_classes:   bitmask(CC_PASSENGERS, CC_REFRIGERATED);      \
        loading_speed:                  5;                                            \
        cost_factor:                    0;                                            \
        running_cost_factor:            0;                                            \
                                                                                      \
        sprite_id:                      SPRITE_ID_NEW_ROADVEH;                        \
        misc_flags:                     tram ? bitmask(ROADVEH_FLAG_TRAM) : 0;        \
        refit_cost:                     0;                                            \
        running_cost_base:              RUNNING_COST_NONE;                            \
        power:                          0;                                            \
        weight:                         0;                                            \
        tractive_effort_coefficient:    0;                                            \
        cargo_capacity:                 20;                                           \
        visual_effect:                  VISUAL_EFFECT_DISABLE;                        \
        length:                         veh_length;                                   \
    }                                                                                 \
                                                                                      \
    graphics {                                                                        \
        default: spritegroup_##veh_length;                                            \
    }                                                                                 \
}

/* Define articulated parts for all lengths with the names ARTICPARTNAME(tram, veh_length) */
ARTICPART(88, 0, 8)
ARTICPART(89, 0, 7)
ARTICPART(90, 0, 6)
ARTICPART(91, 0, 5)
ARTICPART(92, 0, 4)
ARTICPART(93, 0, 3)
ARTICPART(94, 0, 2)
ARTICPART(95, 0, 1)

ARTICPART(188, 1, 8)
ARTICPART(189, 1, 7)
ARTICPART(190, 1, 6)
ARTICPART(191, 1, 5)
ARTICPART(192, 1, 4)
ARTICPART(193, 1, 3)
ARTICPART(194, 1, 2)
ARTICPART(195, 1, 1)

#define RV_BASE(id, veh_name, tram, front_length, switches)                           \
item(FEAT_ROADVEHS, item_rv_##tram##_##veh_name, id) {                                \
    property {                                                                        \
        name:                           string(veh_name);                             \
        climates_available:             ALL_CLIMATES;                                 \
        introduction_date:              date(01,01,01);                               \
        model_life:                     255;                                          \
        vehicle_life:                   15;                                           \
        reliability_decay:              20;                                           \
        refittable_cargo_classes:       bitmask(CC_PIECE_GOODS, CC_EXPRESS);          \
        non_refittable_cargo_classes:   bitmask(CC_PASSENGERS, CC_REFRIGERATED);      \
        loading_speed:                  5;                                            \
        cost_factor:                    1;                                            \
        running_cost_factor:            1;                                            \
                                                                                      \
        sprite_id:                      SPRITE_ID_NEW_ROADVEH;                        \
        speed:                          50 km/h;                                      \
        misc_flags:                     tram ? bitmask(ROADVEH_FLAG_TRAM) : 0;        \
        refit_cost:                     0;                                            \
        running_cost_base:              RUNNING_COST_ROADVEH;                         \
        power:                          200 hp;                                       \
        weight:                         2 ton;                                        \
        tractive_effort_coefficient:    0.3;                                          \
        cargo_capacity:                 20;                                           \
        visual_effect:                  VISUAL_EFFECT_DISABLE;                        \
        length:                         front_length;                                 \
    }                                                                                 \
                                                                                      \
    graphics {                                                                        \
        default: spritegroup_##front_length;                                          \
        switches                                                                      \
    }                                                                                 \
}

/* Macro for defining a non-articulated RV (both normal and tram at once) */
#define RV_1PART(id, veh_name, front_length)                                          \
    RV_BASE(id, veh_name, 0, front_length, )                                          \
    RV_BASE(id + 100, veh_name, 1, front_length, )

/* Macro for defining an articulared RV of any length using a custom switch block (both normal and tram at once) */
#define RV_XPART(id, veh_name, front_length, artic_switch)                            \
    RV_BASE(id, veh_name, 0, front_length, articulated_part: artic_switch##_normal;)  \
    RV_BASE(id + 100, veh_name, 1, front_length, articulated_part: artic_switch##_tram;)

/* Macro for defining an articulated RV with 2 parts (both normal and tram at once) */
#define RV_2PART(id, veh_name, len1, len2)                                            \
switch (FEAT_ROADVEHS, SELF, switch_articulated_##veh_name##_normal, extra_callback_info1) { \
    1: return ARTICPARTNAME(0, len2);                                                 \
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;                                       \
}                                                                                     \
switch (FEAT_ROADVEHS, SELF, switch_articulated_##veh_name##_tram, extra_callback_info1) { \
    1: return ARTICPARTNAME(1, len2);                                                 \
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;                                       \
}                                                                                     \
RV_XPART(id, veh_name, len1, switch_articulated_##veh_name)

/* Macro for defining an articulated RV with 3 parts (both normal and tram at once) */
#define RV_3PART(id, veh_name, len1, len2, len3)                                      \
switch (FEAT_ROADVEHS, SELF, switch_articulated_##veh_name##_normal, extra_callback_info1) { \
    1: return ARTICPARTNAME(0, len2);                                                 \
    2: return ARTICPARTNAME(0, len3);                                                 \
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;                                       \
}                                                                                     \
switch (FEAT_ROADVEHS, SELF, switch_articulated_##veh_name##_tram, extra_callback_info1) { \
    1: return ARTICPARTNAME(1, len2);                                                 \
    2: return ARTICPARTNAME(1, len3);                                                 \
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;                                       \
}                                                                                     \
RV_XPART(id, veh_name, len1, switch_articulated_##veh_name)
